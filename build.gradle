plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id "io.freefair.lombok" version "8.10"
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.8"
}

group = 'es.degrassi'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly 'net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT'
    implementation "dev.dejvokep:boosted-yaml:1.3.7"
    implementation 'org.bstats:bstats-bungeecord:3.1.0'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

shadowJar {
    minimize()
    relocate 'dev.dejvokep.boostedyaml', 'libs'
    relocate 'org.jetbrains.annotations', 'libs'
    relocate 'org.bstats', 'libs'
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates